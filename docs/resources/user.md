---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "cloudconnexa_user Resource - terraform-provider-cloudconnexa"
subcategory: ""
description: |-
  Use cloudconnexa_user to create an CloudConnexa user.
---

# cloudconnexa_user (Resource)

Use `cloudconnexa_user` to create an CloudConnexa user.

## Example Usage

```terraform
# User groups for different departments
resource "cloudconnexa_user_group" "developers" {
  name                 = "developers"
  description          = "Development team with full access"
  all_regions_included = true
  connect_auth         = "ON_PRIOR_AUTH"
  internet_access      = "SPLIT_TUNNEL_ON"
  max_device           = "5"
}

resource "cloudconnexa_user_group" "admins" {
  name                 = "admins"
  description          = "System administrators"
  all_regions_included = true
  connect_auth         = "EVERY_TIME"
  internet_access      = "SPLIT_TUNNEL_OFF"
  max_device           = "3"
}

resource "cloudconnexa_user_group" "contractors" {
  name                 = "contractors"
  description          = "External contractors with limited access"
  all_regions_included = false
  connect_auth         = "EVERY_TIME"
  internet_access      = "RESTRICTED_INTERNET"
  max_device           = "2"
}

# Admin user example
resource "cloudconnexa_user" "admin_user" {
  username   = "admin.user"
  group_id   = cloudconnexa_user_group.admins.id
  role       = "ADMIN"
  first_name = "Admin"
  last_name  = "User"
  email      = "admin@example.com"
}

# Developer with device management
resource "cloudconnexa_user" "developer" {
  username   = "john.developer"
  group_id   = cloudconnexa_user_group.developers.id
  role       = "MEMBER"
  first_name = "John"
  last_name  = "Developer"
  email      = "john.developer@example.com"

  devices {
    name         = "Johns-Laptop"
    description  = "MacBook Pro for development"
    ipv4_address = "10.0.1.100"
  }
}

# Contractor user with limited permissions
resource "cloudconnexa_user" "contractor" {
  username   = "jane.contractor"
  group_id   = cloudconnexa_user_group.contractors.id
  role       = "MEMBER"
  first_name = "Jane"
  last_name  = "Contractor"
  email      = "jane.contractor@external.com"

  devices {
    name        = "Contractor-Device"
    description = "Temporary device for project work"
  }
}

# Suspended user example - temporarily disable access
resource "cloudconnexa_user" "suspended_user" {
  username   = "suspended.user"
  group_id   = cloudconnexa_user_group.contractors.id
  role       = "MEMBER"
  first_name = "Suspended"
  last_name  = "User"
  email      = "suspended@example.com"
  status     = "SUSPENDED" # User cannot connect when suspended
}

# User that can be toggled between active and suspended
variable "user_active" {
  description = "Whether the temporary user should be active"
  type        = bool
  default     = true
}

resource "cloudconnexa_user" "toggleable_user" {
  username   = "toggleable.user"
  group_id   = cloudconnexa_user_group.contractors.id
  role       = "MEMBER"
  first_name = "Toggleable"
  last_name  = "User"
  email      = "toggleable@example.com"
  status     = var.user_active ? "ACTIVE" : "SUSPENDED"
}

# Basic user without email (for testing)
resource "cloudconnexa_user" "basic_user" {
  username = "test.user"
  group_id = cloudconnexa_user_group.developers.id
  role     = "MEMBER"
}

# Multiple users example using for_each
variable "team_members" {
  description = "List of team members to create"
  type = map(object({
    email      = string
    first_name = string
    last_name  = string
    group_id   = string
    role       = string
  }))
  default = {
    "alice.smith" = {
      email      = "alice.smith@example.com"
      first_name = "Alice"
      last_name  = "Smith"
      group_id   = "developers"
      role       = "MEMBER"
    }
    "bob.johnson" = {
      email      = "bob.johnson@example.com"
      first_name = "Bob"
      last_name  = "Johnson"
      group_id   = "developers"
      role       = "MEMBER"
    }
  }
}

resource "cloudconnexa_user" "team_members" {
  for_each = var.team_members

  username   = each.key
  first_name = each.value.first_name
  last_name  = each.value.last_name
  email      = each.value.email
  group_id   = each.value.group_id == "developers" ? cloudconnexa_user_group.developers.id : cloudconnexa_user_group.contractors.id
  role       = each.value.role
}

# Outputs
output "user_groups" {
  description = "Created user groups"
  value = {
    developers  = cloudconnexa_user_group.developers.id
    admins      = cloudconnexa_user_group.admins.id
    contractors = cloudconnexa_user_group.contractors.id
  }
}

output "users" {
  description = "Created users"
  value = {
    admin      = cloudconnexa_user.admin_user.id
    developer  = cloudconnexa_user.developer.id
    contractor = cloudconnexa_user.contractor.id
    basic      = cloudconnexa_user.basic_user.id
  }
}

output "team_member_ids" {
  description = "Team member user IDs"
  value       = { for k, v in cloudconnexa_user.team_members : k => v.id }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `group_id` (String) The UUID of a user's group.
- `username` (String) A username for the user.

### Optional

- `devices` (Block List, Max: 1) When a user signs in, the device that they use will be added to their account. You can read more at [CloudConnexa Device](https://openvpn.net/cloud-docs/device/). (see [below for nested schema](#nestedblock--devices))
- `email` (String) An invitation to CloudConnexa account will be sent to this email. It will include an initial password and a VPN setup guide.
- `first_name` (String) User's first name.
- `last_name` (String) User's last name.
- `role` (String) The type of user role. Valid values are `ADMIN`, `MEMBER`, or `OWNER`.
- `secondary_groups_ids` (List of String) The UUIDs of secondary user's groups.
- `status` (String) The status of the user. Valid values are `ACTIVE` or `SUSPENDED`. When set to `SUSPENDED`, the user will be suspended and unable to connect.

### Read-Only

- `auth_type` (String) The authentication type of the user.
- `connection_status` (String) The connection status of the user.
- `id` (String) The ID of this resource.

<a id="nestedblock--devices"></a>
### Nested Schema for `devices`

Required:

- `description` (String) A device description.
- `name` (String) A device name.

Optional:

- `ipv4_address` (String) An IPv4 address of the device.
- `ipv6_address` (String) An IPv6 address of the device.

## Import

Import is supported using the following syntax:

The [`terraform import` command](https://developer.hashicorp.com/terraform/cli/commands/import) can be used, for example:

```shell
terraform import cloudconnexa_user.example <username@cloud_id>
```
